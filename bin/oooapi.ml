open Ppxlib

let loc = Location.in_file "ooo_spec_gen.ml"

module Ast = Ast_builder.Make (struct
  let loc = loc
end)

(* let context = Extension.Context.module_expr *)
(* let name = "openapi.client" *)
type endpoint = string * string * string

(** An AST node for a name v *)
let var v = Ast.Located.mk v

(** Construct the AST node of an endpoint function *)
let endpoint (fname, uri, data) : structure_item =
  let function_name = Ast.ppat_var (var fname) in
  let uri = Ast.estring uri in
  let data = Ast.estring data in
  [%stri let [%p function_name] = Client.request [%e uri] [%e data]]

(** Construct the AST node of the module with all endpoint functions *)
let endpoint_module : name:string -> endpoint list -> structure_item =
 fun ~name endpoints ->
  let name = Ast.Located.mk (Some (name ^ "_api")) in
  let expr = endpoints |> List.map endpoint |> Ast.pmod_structure in
  Ast.module_binding ~name ~expr |> Ast.pstr_module

(* TODO: name and endpoints to become all data from OpenAPI spec *)
let modules name endpoints = [%str [%%i endpoint_module ~name endpoints]]

(* TODO: Put in comment *)
let header = "Generated by oooapi on DATE"

let write_ast str =
  let fmt = Format.std_formatter in
  Format.fprintf fmt "%a\n" Pprintast.structure str;
  Format.print_flush ()

let module_of_spec : Openapi_spec.t -> Ppxlib.Ast.structure =
 fun _spec ->
  modules
    "OpenAI"
    [ ("start_chat", "chat", "mydata")
    ; ("end_chat", "chat/end", "foo")
    ; ("completion", "complete", "mycompletiondata")
    ]

let () =
  let spec_file = ref "" in
  Arg.parse [] (fun f -> spec_file := f) "oooapi <spec_file>";
  let spec =
    match !spec_file with
    | "" -> failwith "argument spec_file must be provided" (* TODO *)
    | f ->
    match Openapi_spec.from_file f with
    | Error (`Msg m) ->
        Printf.eprintf "error: could not parse spec %s\n%s" f m;
        exit 1
    | Ok s -> s
  in
  spec |> module_of_spec |> write_ast
